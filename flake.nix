{
  description = "Quickshell-based image, video & Wallpaper Image wallpaper selector with color sorting, Matugen integration, and more";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-unstable";
    quickshell.url = "github:quickshell-mirror/quickshell";
    awww.url = "git+https://codeberg.org/LGFae/awww";
    skwd-daemon.url = "github:liixini/skwd-daemon";
  };

  outputs = { self, nixpkgs, quickshell, awww, skwd-daemon, ... }:
    let
      forEachSystem = nixpkgs.lib.genAttrs [ "x86_64-linux" "aarch64-linux" ];
    in {
      packages = forEachSystem (system:
        let
          pkgs = nixpkgs.legacyPackages.${system};
          qsPkgs = quickshell.inputs.nixpkgs.legacyPackages.${system};

          quickshellWithModules = quickshell.packages.${system}.default.withModules (with qsPkgs.qt6; [
            qtimageformats
            qtmultimedia
            qtsvg
            qt5compat
            qtwayland
          ]);

          daemon = skwd-daemon.packages.${system}.default;

          runtimeDeps = with pkgs; [
            daemon
            matugen
            ffmpeg
            imagemagick
            inotify-tools
            sqlite
            curl
            file
            mpvpaper
            jq
            awww.packages.${system}.awww
          ];

          daemonDeps = runtimeDeps ++ [ quickshellWithModules ];

          fonts = with pkgs; [
            nerd-fonts.symbols-only
            roboto
            roboto-mono
            material-design-icons
          ];

          # ── skwd-tracker (dwl-ipc subscriber, Python) ─────────────────────
          # Generates pywayland bindings for zdwl_ipc_unstable_v2 from the
          # vendored XML, then wraps tracker.py as the `skwd-tracker` binary.
          dwlIpcBindings = pkgs.runCommand "dwl-ipc-pywayland" {
            nativeBuildInputs = [ pkgs.python3Packages.pywayland pkgs.pkg-config pkgs.wayland.dev ];
          } ''
            mkdir -p $out
            # Pass wayland.xml first so cross-protocol references (wl_output)
            # resolve. The scanner emits the wayland module too — drop it,
            # pywayland's built-in version provides the canonical one. Then
            # rewrite relative imports (`from ..wayland import`) to absolute
            # (`from pywayland.protocol.wayland import`) since our generated
            # module isn't installed *inside* pywayland's protocol package.
            pywayland-scanner \
              -i ${pkgs.wayland-scanner}/share/wayland/wayland.xml \
                 ${./protocols/dwl-ipc-unstable-v2.xml} \
              -o $out
            rm -rf $out/wayland
            ${pkgs.gnused}/bin/sed -i \
              's|from \.\.wayland import|from pywayland.protocol.wayland import|g' \
              $out/dwl_ipc_unstable_v2/*.py
          '';

          trackerPython = pkgs.python3.withPackages (ps: [ ps.pywayland ]);
        in {
          default = pkgs.stdenv.mkDerivation {
            pname = "skwd-wall";
            version = "unstable";
            src = ./.;

            nativeBuildInputs = [ pkgs.makeWrapper ];

            installPhase = ''
              mkdir -p $out/share/skwd-wall
              cp -a shell.qml qml/ $out/share/skwd-wall/

              mkdir -p $out/share/skwd-wall/data
              cp -a data/matugen/ $out/share/skwd-wall/data/
              cp -a data/scripts/ $out/share/skwd-wall/data/
              install -Dm644 data/config.json.example $out/share/skwd-wall/data/config.json.example

              install -Dm644 data/skwd-wall.desktop $out/share/applications/skwd-wall.desktop

              makeWrapper ${quickshellWithModules}/bin/quickshell $out/bin/skwd-wall \
                --prefix PATH : ${pkgs.lib.makeBinPath runtimeDeps} \
                --add-flags "-p $out/share/skwd-wall/shell.qml"

              # skwd-tracker: dwl-ipc subscriber that pushes focus changes to
              # ~/.cache/skwd-wall/active-monitor. Pair with `monitor: "auto"`.
              install -Dm644 tracker.py $out/share/skwd-wall/tracker.py
              makeWrapper ${trackerPython}/bin/python $out/bin/skwd-tracker \
                --add-flags $out/share/skwd-wall/tracker.py \
                --prefix PYTHONPATH : ${dwlIpcBindings}

              makeWrapper ${daemon}/bin/skwd $out/bin/skwd \
                --prefix PATH : ${pkgs.lib.makeBinPath daemonDeps} \
                --set SKWD_SHELL_QML "$out/share/skwd-wall/shell.qml" \
                --set SKWD_DATA_DIR "$out/share/skwd-wall/data"

              makeWrapper ${daemon}/bin/skwd-daemon $out/bin/skwd-daemon \
                --prefix PATH : ${pkgs.lib.makeBinPath daemonDeps} \
                --set SKWD_SHELL_QML "$out/share/skwd-wall/shell.qml" \
                --set SKWD_DATA_DIR "$out/share/skwd-wall/data"

              mkdir -p $out/lib/systemd/user
              substitute ${daemon}/lib/systemd/user/skwd-daemon.service \
                $out/lib/systemd/user/skwd-daemon.service \
                --replace-fail "${daemon}/bin/skwd-daemon" "$out/bin/skwd-daemon"

              install -Dm644 LICENSE $out/share/licenses/skwd-wall/LICENSE

              mkdir -p $out/share/fonts
              for font in ${pkgs.lib.concatMapStringsSep " " toString fonts}; do
                if [ -d "$font/share/fonts" ]; then
                  for f in $(find "$font/share/fonts" -type f); do
                    ln -sf "$f" "$out/share/fonts/$(basename $f)"
                  done
                fi
              done
            '';

            meta = {
              description = "Quickshell-based image, video & Wallpaper Image wallpaper selector with color sorting, Matugen integration, and more";
              homepage = "https://github.com/liixini/skwd-wall";
              license = pkgs.lib.licenses.mit;
              mainProgram = "skwd-wall";
            };
          };
        });
    };
}
